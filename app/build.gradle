apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-kapt"
apply plugin: "dagger.hilt.android.plugin"
apply plugin: "com.mikepenz.aboutlibraries.plugin"
apply plugin: "com.getkeepsafe.dexcount"

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion rootProject.ext.COMPILE_SDK

    defaultConfig {
        applicationId "de.mchllngr.quickero"
        minSdkVersion rootProject.ext.MIN_SDK
        targetSdkVersion rootProject.ext.TARGET_SDK
        versionCode rootProject.ext.VERSIONCODE
        versionName rootProject.ext.VERSIONNAME

        resConfigs "en", "de"
    }

    buildFeatures {
        viewBinding true
    }

    signingConfigs {
        release {
            storeFile file(keystoreProperties["storeFile"])
            storePassword keystoreProperties["storePassword"]
            keyAlias keystoreProperties["keyAlias"]
            keyPassword keystoreProperties["keyPassword"]
        }
    }

    buildTypes {
        release {
            postprocessing {
                removeUnusedCode true
                removeUnusedResources true
                obfuscate true
                optimizeCode true
                proguardFiles getDefaultProguardFile("proguard-defaults.txt"),
                    "../proguard/proguard-rules.pro",
                    "../proguard/proguard-rules-aboutlibraries.pro",
                    "../proguard/proguard-rules-timber.pro"
            }
            signingConfig signingConfigs.release
        }

        debug {
            postprocessing {
                removeUnusedCode false
                removeUnusedResources false
                obfuscate false
                optimizeCode false
            }

            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
    }

    dexOptions {
        maxProcessCount 4
        javaMaxHeapSize "3g"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += [
            "-Xjvm-default=compatibility"
        ]
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$KOTLIN_VERSION"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$KOTLIN_COROUTINES_VERSION"

    implementation "androidx.core:core-ktx:$ANDROIDX_CORE_KTX_VERSION"
    implementation "androidx.fragment:fragment-ktx:$ANDROIDX_FRAGMENT_KTX_VERSION"
    implementation "androidx.recyclerview:recyclerview:$ANDROIDX_RECYCLERVIEW_VERSION"
    implementation "com.google.android.material:material:$ANDROIDX_MATERIAL_VERSION"
    implementation "androidx.datastore:datastore-preferences:$ANDROIDX_DATASTORE_VERSION"

    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$ANDROIDX_LIFECYCLE_LIVEDATA_KTX_VERSION"

    implementation "com.google.dagger:dagger:$DAGGER_VERSION"
    kapt "com.google.dagger:dagger-compiler:$DAGGER_VERSION"
    implementation "com.google.dagger:hilt-android:$DAGGER_HILT_VERSION"
    kapt "com.google.dagger:hilt-android-compiler:$DAGGER_HILT_VERSION"

    implementation "com.jakewharton.timber:timber:$TIMBER_VERSION"

    implementation "com.afollestad.material-dialogs:core:$MATERIAL_DIALOGS_VERSION"
    implementation "com.afollestad.material-dialogs:bottomsheets:$MATERIAL_DIALOGS_VERSION"
    implementation "com.afollestad.material-dialogs:lifecycle:$MATERIAL_DIALOGS_VERSION"

    implementation "com.mikepenz:aboutlibraries:$ABOUT_LIBRARIES_VERSION"

    // Debug dependencies
    debugImplementation "com.github.pandulapeter.beagle:ui-drawer:$BEAGLE_VERSION"
    debugImplementation "com.github.pandulapeter.beagle:log:$BEAGLE_VERSION"
}

kapt {
    javacOptions {
        // These options are normally set automatically via the Hilt Gradle plugin, but we
        // set them manually to workaround a bug in the Kotlin 1.5.20
        // https://stackoverflow.com/a/68233842/4567591
        option("-Adagger.fastInit=ENABLED")
        option("-Adagger.hilt.android.internal.disableAndroidSuperclassValidation=true")
    }
}
