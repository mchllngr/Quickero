boolean CI_BUILD = hasProperty('ciBuild') ? ciBuild.toBoolean() : false
println "CI_BUILD: " + CI_BUILD

apply plugin: 'com.android.application'
apply plugin: 'com.getkeepsafe.dexcount'
if (!CI_BUILD) apply plugin: 'io.fabric'

android {
    compileSdkVersion rootProject.ext.COMPILE_SDK_VERSION
    buildToolsVersion rootProject.ext.BUILD_TOOLS_VERSION

    dexOptions {
        maxProcessCount 4
        javaMaxHeapSize "3g"
    }

    defaultConfig {
        applicationId "de.mchllngr.quickopen"
        minSdkVersion rootProject.ext.MIN_SDK_VERSION
        targetSdkVersion rootProject.ext.TARGET_SDK_VERSION
        versionCode rootProject.ext.VERSIONCODE
        versionName rootProject.ext.VERSIONAME

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {
            storeFile file("../keystore/debug-keystore.jks")
            storePassword "debug-keystore"
            keyAlias "debug-keystore"
            keyPassword "debug-keystore"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
        debug {
            minifyEnabled false
            shrinkResources false
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            signingConfig signingConfigs.debug
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }
}

dependencies {
    implementation "com.android.support:appcompat-v7:$rootProject.ext.SUPPORT_VERSION"
    implementation "com.android.support:design:$rootProject.ext.SUPPORT_VERSION"
    implementation "com.android.support:recyclerview-v7:$rootProject.ext.SUPPORT_VERSION"
    implementation "com.android.support:preference-v7:$rootProject.ext.SUPPORT_VERSION"

    implementation "com.hannesdorfmann.mosby:mvp:$rootProject.ext.MOSBY_VERSION"

    implementation "com.jakewharton:butterknife:$rootProject.ext.BUTTERKNIFE_VERSION"
    annotationProcessor "com.jakewharton:butterknife-compiler:$rootProject.ext.BUTTERKNIFE_VERSION"

    implementation "com.jakewharton.timber:timber:$rootProject.ext.TIMBER_VERSION"

    implementation "com.google.code.gson:gson:$rootProject.ext.GSON_VERSION"

    implementation "io.reactivex:rxjava:$rootProject.ext.RX_JAVA_VERSION"
    implementation "io.reactivex:rxandroid:$rootProject.ext.RX_ANDROID_VERSION"

    implementation "com.f2prateek.rx.preferences:rx-preferences:$rootProject.ext.RX_PREFERENCES_VERSION"

    implementation "com.afollestad.material-dialogs:core:$rootProject.ext.MATERIAL_DIALOGS_VERSION"
    implementation "com.afollestad.material-dialogs:commons:$rootProject.ext.MATERIAL_DIALOGS_VERSION"

    implementation "com.bignerdranch.android:simple-item-decoration:$rootProject.ext.SIMPLE_ITEM_DECORATION_VERSION"

    implementation "com.github.medyo:android-about-page:$rootProject.ext.ABOUT_PAGE_VERSION"

    implementation "com.crashlytics.sdk.android:crashlytics:$rootProject.ext.CRASHLYTICS_VERSION"

    implementation "com.google.firebase:firebase-core:$rootProject.ext.FIREBASE_VERSION"

    debugImplementation "com.facebook.stetho:stetho:$rootProject.ext.STETHO_VERSION"

    debugImplementation "io.palaima.debugdrawer:debugdrawer:$rootProject.ext.DEBUGDRAWER_VERSION"
    debugImplementation "io.palaima.debugdrawer:debugdrawer-commons:$rootProject.ext.DEBUGDRAWER_VERSION"
    debugImplementation "io.palaima.debugdrawer:debugdrawer-actions:$rootProject.ext.DEBUGDRAWER_VERSION"
    debugImplementation "io.palaima.debugdrawer:debugdrawer-scalpel:$rootProject.ext.DEBUGDRAWER_VERSION"
    debugImplementation "io.palaima.debugdrawer:debugdrawer-fps:$rootProject.ext.DEBUGDRAWER_VERSION"
    // Used by DebugDrawer
    debugImplementation "com.jakewharton.scalpel:scalpel:$rootProject.ext.SCALPEL_VERSION"
    debugImplementation "jp.wasabeef:takt:$rootProject.ext.TAKT_VERSION"

    testImplementation "junit:junit:$rootProject.ext.JUNIT_VERSION"
    testImplementation "org.mockito:mockito-core:$rootProject.ext.MOCKITO_VERSION"

    androidTestImplementation("com.android.support.test.espresso:espresso-core:$rootProject.ext.ESPRESSO_VERSION", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}

apply plugin: 'com.google.gms.google-services'
