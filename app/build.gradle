apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.getkeepsafe.dexcount'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion SdkVersions.COMPILE_SDK

    dexOptions {
        maxProcessCount 4
        javaMaxHeapSize "3g"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += [
            "-Xjvm-default=compatibility"
        ]
    }

    defaultConfig {
        applicationId "de.mchllngr.quickero"
        minSdkVersion SdkVersions.MIN_SDK
        targetSdkVersion SdkVersions.TARGET_SDK
        versionCode rootProject.ext.VERSIONCODE
        versionName rootProject.ext.VERSIONNAME

        resConfigs "en", "de"
    }

    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'] ?: "UNKNOWN")
            storePassword keystoreProperties['storePassword'] ?: "UNKNOWN"
            keyAlias keystoreProperties['keyAlias'] ?: "UNKNOWN"
            keyPassword keystoreProperties['keyPassword'] ?: "UNKNOWN"
        }
    }

    buildTypes {
        release {
            postprocessing {
                removeUnusedCode true
                removeUnusedResources true
                obfuscate true
                optimizeCode true
                proguardFiles getDefaultProguardFile('proguard-defaults.txt'),
                    '../proguard/proguard-rules.pro',
                    '../proguard/proguard-rules-butterknife.pro',
                    '../proguard/proguard-rules-gson.pro',
                    '../proguard/proguard-rules-rxjava.pro',
                    '../proguard/proguard-rules-timber.pro'
            }
            signingConfig signingConfigs.release
        }

        debug {
            postprocessing {
                removeUnusedCode false
                removeUnusedResources false
                obfuscate false
                optimizeCode false
            }

            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
    }

    sourceSets {
        main {
            // for merging res-folders together
            res.srcDirs = ['src/main/res']
//            file('src/main/res-modules')
//                .eachDir { res.srcDirs += it.path }
        }
    }
}

dependencies {
    implementation Dependencies.KOTLIN_STDLIB
    implementation Dependencies.KOTLIN_COROUTINES_ANDROID

    implementation Dependencies.ANDROIDX_CORE_KTX
    implementation Dependencies.ANDROIDX_FRAGMENT_KTX
    implementation Dependencies.ANDROIDX_APPCOMPAT
    implementation Dependencies.ANDROIDX_RECYCLERVIEW
    implementation Dependencies.ANDROIDX_MATERIAL
    implementation Dependencies.ANDROIDX_CONSTRAINTLAYOUT
    implementation Dependencies.ANDROIDX_PREFERENCE

    implementation Dependencies.ANDROIDX_LIFECYCLE_EXTENSIONS
    implementation Dependencies.ANDROIDX_LIFECYCLE_COMMON
    implementation Dependencies.ANDROIDX_LIFECYCLE_LIVEDATA_KTX
    implementation Dependencies.ANDROIDX_LIFECYCLE_VIEWMODEL_KTX

    implementation Dependencies.MOSBY

    implementation Dependencies.BUTTERKNIFE
    kapt Dependencies.BUTTERKNIFE_COMPILER

    implementation Dependencies.TIMBER

    implementation Dependencies.GSON

    implementation Dependencies.RX_JAVA
    implementation Dependencies.RX_ANDROID

    implementation Dependencies.RX_PREFERENCES

    implementation Dependencies.MATERIAL_DIALOGS_CORE
    implementation Dependencies.MATERIAL_DIALOGS_BOTTOMSHEETS
    implementation Dependencies.MATERIAL_DIALOGS_LIFECYCLE

    implementation Dependencies.SIMPLE_ITEM_DECORATION

    implementation Dependencies.ABOUT_PAGE

    // Debug dependencies
    debugImplementation DebugDependencies.BEAGLE
    debugImplementation DebugDependencies.BEAGLE_LOG
}
